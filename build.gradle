buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'io.codearte.nexus-staging' version '0.11.0'
}

apply plugin: 'com.bmuschko.nexus'
apply plugin: 'java'

group = 'com.ganwhat.hummingbird'
archivesBaseName = 'hummingbird-resources'
version = '0.0.5-SNAPSHOT'

modifyPom {
    project {
        artifactId "${gradle.rootProject.archivesBaseName}"
        name "${gradle.rootProject.archivesBaseName}"
        description 'Resources for hummingbird'
        url 'https://github.com/cvvnx1/hummingbird-resources'
        inceptionYear '2019'
        scm {
            url 'https://github.com/cvvnx1/hummingbird-resources'
            connection 'scm:https://github.com/cvvnx1/hummingbird-resources.git'
            developerConnection 'scm:https://github.com/cvvnx1/hummingbird-resources.git'
        }
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'cvvnx1'
                name 'Tony He'
                email 'cvvnx1@hotmail.com'
            }
        }
    }
}

jar {
    manifest {
        attributes(
                'Manifest-Group': "${gradle.rootProject.group}",
                'Manifest-Artifact': "${gradle.rootProject.archivesBaseName}",
                'Manifest-Version': "${gradle.rootProject.version}",
                'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Built-By': 'cvvnx1@hotmail.com',
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                'Created-By': "Gradle ${gradle.gradleVersion}"
        )
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

ext {
    lombok = 'org.projectlombok:lombok:1.18.8'
}

dependencies {
    compileOnly lombok
    annotationProcessor lombok

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'

    compile 'org.mousio:etcd4j:2.17.0'
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}
